

///////////////////////////////////\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\
///////////////////////Обработчики Событий Формы \\\\\\\\\\\\\\\\\\\\\\\\\\
////////////////\\\\\\\\\\\\\\\///////////////\\\\\\\\\\\\\\///////////////

#Область СобытияФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ИмяШага = ?(Параметры.Свойство("ИмяШага",ИмяШага),ИмяШага,"");
	МетодПредварительныхДействий = ?(Параметры.Свойство("МетодПредварительныхДействий",МетодПредварительныхДействий),МетодПредварительныхДействий,"");
	МетодПостПроверки = ?(Параметры.Свойство("МетодПостПроверки",МетодПостПроверки),МетодПостПроверки,"");
	ШаблонныеДанныеДляПостПроверки = ?(Параметры.Свойство("ШаблонныеДанныеДляПостПроверки",ШаблонныеДанныеДляПостПроверки),ШаблонныеДанныеДляПостПроверки,Неопределено);
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	Элементы.РедактироватьXMLШаблона.Видимость = (ШаблонныеДанныеДляПостПроверки <> Неопределено);
КонецПроцедуры

//@skip-check module-empty-method
&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
				
КонецПроцедуры




#КонецОбласти


///////////////////////////////////\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\
///////////////////////Обработчики Событий Команд\\\\\\\\\\\\\\\\\\\\\\\\\\
////////////////\\\\\\\\\\\\\\\///////////////\\\\\\\\\\\\\\///////////////

#Область КомандыФормы

&НаКлиенте
Процедура Подтвердить(Команда)
	Если ПустаяСтрока(ИмяШага) Тогда 
		КиурКл.ОтобразитьПредупреждение("Не заполнено ИмяШага.",ЭтаФорма);
		Возврат;
	КонецЕсли;
	
	Если ПустаяСтрока(МетодПостПроверки) И ШаблонныеДанныеДляПостПроверки = Неопределено Тогда
		КиурКл.ОтобразитьПредупреждение("Не определены ШаблонныеДанные для пост проверки по умолчанию.",ЭтаФорма);
		Возврат;		
	КонецЕсли;	
	
	СтруктураВозврата = новый Структура;
	СтруктураВозврата.Вставить("ИмяШага",ИмяШага);
	СтруктураВозврата.Вставить("МетодПредварительныхДействий",МетодПредварительныхДействий);
	СтруктураВозврата.Вставить("МетодПостПроверки",МетодПостПроверки);
	СтруктураВозврата.Вставить("ШаблонныеДанныеДляПостПроверки",ШаблонныеДанныеДляПостПроверки);
	
	Закрыть(СтруктураВозврата);	
КонецПроцедуры

&НаКлиенте
Процедура СгенерироватьШаблонныеДанные(Команда)
	ТранзитныеПараметры = новый Структура;
	ОповещениеЗакрытия = новый ОписаниеОповещения("ЗакрытияОкнаГенерацииШаблонныхДанных",ЭтаФорма,ТранзитныеПараметры);
	
	
	ПараметрыОткрытия = новый Структура;
	Если ТипЗнч(ШаблонныеДанныеДляПостПроверки) = Тип("ХранилищеЗначения") Тогда
		СтруктураХранилища = КиурСрв.ВернутьЗначениеХранилища(ШаблонныеДанныеДляПостПроверки);
		ПараметрыОткрытия.Вставить("СхемаВыгрузкиШаблонныхДанных",КиурКлСрв.Десериализовать_из_XML_Строки(СтруктураХранилища.СхемаВыгрузкиШаблонныхДанных));
		ПараметрыОткрытия.Вставить("ШаблонныеДанные",СтруктураХранилища.ШаблонныеДанные);		
	КонецЕсли;	
	
	ОткрытьФорму("ОбщаяФорма.Tester_ФормаУправленияШаблоннымиДанными",ПараметрыОткрытия,ЭтаФорма,,,,ОповещениеЗакрытия,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
КонецПроцедуры

&НаКлиенте
Процедура ЗакрытияОкнаГенерацииШаблонныхДанных(РезультатЗакрытия,ТранзитныеПараметры) ЭКСПОРТ
	Если ТипЗнч(РезультатЗакрытия) <> Тип("Структура") Тогда
		Возврат;
	КонецЕсли;
	
	СтруктураХранилища = новый Структура;
	СтруктураХранилища.Вставить("СхемаВыгрузкиШаблонныхДанных",КиурКлСрв.Сериализовать_в_XML_Строку(РезультатЗакрытия.СхемаВыгрузкиШаблонныхДанных));
	СтруктураХранилища.Вставить("ШаблонныеДанные",РезультатЗакрытия.ШаблонныеДанные);
	СтруктураХранилища = новый ФиксированнаяСтруктура(СтруктураХранилища);
	
	ШаблонныеДанныеДляПостПроверки = КиурСрв.СоздатьХранилищеЗначения(СтруктураХранилища,9);
КонецПроцедуры

&НаКлиенте
Процедура РедактироватьXMLШаблона(Команда)
	Если ТипЗнч(ШаблонныеДанныеДляПостПроверки) <> Тип("ХранилищеЗначения") Тогда
		КиурКл.ОтобразитьПредупреждение("Не соответствующий тип шаблонных данных. Попробуйте сгенерировать новые.",ЭтаФорма);
		Возврат;		
	КонецЕсли;
	
	СтруктураХранилища = КиурСрв.ВернутьЗначениеХранилища(ШаблонныеДанныеДляПостПроверки);
	
	ТранзитныеПараметры = новый Структура;
	ТранзитныеПараметры.Вставить("СтруктураХранилища",СтруктураХранилища);
	ОповещениеЗакрытия = новый ОписаниеОповещения("ЗакрытияОкнаРедактированияXMLШаблона",ЭтаФорма,ТранзитныеПараметры);	
	
	ПараметрыОткрытия = новый Структура;
	ПараметрыОткрытия.Вставить("РедактируемыйТекст",СтруктураХранилища.ШаблонныеДанные);
	ОткрытьФорму("ОбщаяФорма.Tester_ФормаРедактированияТекста",ПараметрыОткрытия,ЭтаФорма,,,,ОповещениеЗакрытия,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);	
КонецПроцедуры

&НаКлиенте
Процедура ЗакрытияОкнаРедактированияXMLШаблона(РезультатЗакрытия,ТранзитныеПараметры) ЭКСПОРТ
	Если ТипЗнч(РезультатЗакрытия) <> Тип("ФиксированнаяСтруктура") Тогда
		Возврат;
	КонецЕсли;
	
	СтруктураХранилища = ТранзитныеПараметры.СтруктураХранилища; 
	
	СтруктураХранилища = новый Структура(СтруктураХранилища);
	
	СтруктураХранилища.ШаблонныеДанные = РезультатЗакрытия.РедактируемыйТекст;
	
	СтруктураХранилища = новый ФиксированнаяСтруктура(СтруктураХранилища);
	
	ШаблонныеДанныеДляПостПроверки = КиурСрв.СоздатьХранилищеЗначения(СтруктураХранилища,9);
КонецПроцедуры

#КонецОбласти

///////////////////////////////////\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\
///////////////////////Обработчики Событий Элементов\\\\\\\\\\\\\\\\\\\\\\\
////////////////\\\\\\\\\\\\\\\///////////////\\\\\\\\\\\\\\///////////////

#Область ЭлементыФормы

#КонецОбласти

///////////////////////////////////\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\
////////////////////////////Служебные методы\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\
////////////////\\\\\\\\\\\\\\\///////////////\\\\\\\\\\\\\\///////////////

#Область Служебные

#КонецОбласти

/////////////////////////////////////\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\
//////////////////////////////////Для удаления\\\\\\\\\\\\\\\\\\\\\\\\\\\\\
/////////////////////////////////////\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\

#Область ДляУдаления

#КонецОбласти

////\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\//////////////////////////////////////
////\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\Для удаления/////////////////////////////
////\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\\//////////////////////////////////////
