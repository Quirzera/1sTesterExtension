Функция ЗаписатьШаблонныеДанныеОбъекта (ФиксированныеПараметры,МодифицируемыеПараметры) ЭКСПОРТ
	
	UIDОбъекта = ФиксированныеПараметры.UIDОбъекта;
	ХранилищеШаблонныхДанных = ФиксированныеПараметры.ХранилищеШаблонныхДанных;
	НаименованиеШаблона = ФиксированныеПараметры.НаименованиеШаблона;
	
	НаборЗаписей = СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.UIDОбъекта.Установить(UIDОбъекта);
	
	нЗапись = НаборЗаписей.Добавить();
	нЗапись.UIDОбъекта = UIDОбъекта;
	нЗапись.ХранилищеШаблонныхДанных = ХранилищеШаблонныхДанных;
	нЗапись.НаименованиеШаблона = НаименованиеШаблона;
	
	Попытка
		НаборЗаписей.Записать(Истина);	
		Возврат Истина;
	Исключение
		СообщениеОшибки = ОписаниеОшибки();		
		МодифицируемыеПараметры.Вставить("СообщениеОшибки",СообщениеОшибки);
		Возврат Ложь;		
	КонецПопытки;
	
КонецФункции

Функция ПолучитьШаблонныеДанныеОбъекта(ФиксированныеПараметры,МодифицируемыеПараметры = Неопределено) ЭКСПОРТ
	Если ТипЗнч(МодифицируемыеПараметры) <> Тип("Структура") Тогда
		МодифицируемыеПараметры = новый Структура;
	КонецЕсли;	
	
	UIDОбъекта = ФиксированныеПараметры.UIDОбъекта;
	
	Запрос = новый Запрос;
	Запрос.УстановитьПараметр("UIDОбъекта",UIDОбъекта);
	Запрос.Текст = "ВЫБРАТЬ
	               |	Tester_ШаблонныеДанные.ХранилищеШаблонныхДанных КАК ХранилищеШаблонныхДанных
	               |ИЗ
	               |	РегистрСведений.Tester_ШаблонныеДанные КАК Tester_ШаблонныеДанные
	               |ГДЕ
	               |	Tester_ШаблонныеДанные.UIDОбъекта = &UIDОбъекта";
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Количество() = 1 Тогда
		Выборка.Следующий();
		Возврат Выборка.ХранилищеШаблонныхДанных;
	КонецЕсли;
	
	Возврат Неопределено;	
КонецФункции

Функция ВыполненоОбслуживаниеРегистра(ФиксированныеПараметры = Неопределено, МодифицируемыеПараметры = Неопределено ) ЭКСПОРТ
	
	Если ТипЗнч(МодифицируемыеПараметры) <> Тип("Структура") Тогда
		МодифицируемыеПараметры = новый Структура;
	КонецЕсли;
	
	Если ТипЗнч(ФиксированныеПараметры) <> Тип("Структура") И ТипЗнч(ФиксированныеПараметры) <> Тип("ФиксированнаяСтруктура") Тогда
		ФиксированныеПараметры = новый ФиксированнаяСтруктура(новый Структура);		
	КонецЕсли;
	
	Запрос = новый Запрос;
	ЗАпрос.Текст = "ВЫБРАТЬ
	|	Tester_СценарииТестированияСтартовыеДанные.UIDСтартовыхДанных КАК UIDОбъекта,
	|	Tester_СценарииТестированияСтартовыеДанные.ИмяНабораСтартовыхДанных КАК НаименованиеШаблона,
	|	ЛОЖЬ КАК ЭтоШаблоннаяСхема
	|ПОМЕСТИТЬ втИспользуемыеДанные
	|ИЗ
	|	Справочник.Tester_СценарииТестирования.СтартовыеДанные КАК Tester_СценарииТестированияСтартовыеДанные
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Tester_СценарииТестированияШагиСценария.UIDШага,
	|	Tester_СценарииТестированияШагиСценария.ИмяШага,
	|	ИСТИНА
	|ИЗ
	|	Справочник.Tester_СценарииТестирования.ШагиСценария КАК Tester_СценарииТестированияШагиСценария
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Tester_ШаблонныеДанные.UIDОбъекта,
	|	(втИспользуемыеДанные.UIDОбъекта ЕСТЬ NULL
	|	И Tester_ШаблонныеДанные.ЭтоШаблоннаяСхема)
	|	ИЛИ Tester_ШаблонныеДанные.ПредставлениеUID = """" КАК НепривязанныйОбъект
	|ИЗ
	|	РегистрСведений.Tester_ШаблонныеДанные КАК Tester_ШаблонныеДанные
	|		ЛЕВОЕ СОЕДИНЕНИЕ втИспользуемыеДанные КАК втИспользуемыеДанные
	|		ПО Tester_ШаблонныеДанные.UIDОбъекта = втИспользуемыеДанные.UIDОбъекта";
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		Если Выборка.НепривязанныйОбъект Тогда
			ПараметрыУдаления = новый Структура;
			ПараметрыУдаления.Вставить("UIDОбъекта",Выборка.UIDОбъекта);
			ПараметрыУдаления = новый ФиксированнаяСтруктура(ПараметрыУдаления);
			УдалитьШаблонныеДанные(ПараметрыУдаления);
		КонецЕсли;
			
	КонецЦикла;
		
	Возврат Истина;
			
КонецФункции

Функция ИзменитьНаименованиеШаблонныхДанныхОбъекта(ФиксированныеПараметры, МодифицируемыеПараметры = Неопределено ) ЭКСПОРТ
	Если ТипЗнч(МодифицируемыеПараметры) <> Тип("Структура") Тогда
		МодифицируемыеПараметры = новый Структура;
	КонецЕсли;
	
	ХранилищеШаблонныхДанных = ПолучитьШаблонныеДанныеОбъекта(ФиксированныеПараметры);
	
	Если ХранилищеШаблонныхДанных = Неопределено Тогда
		МодифицируемыеПараметры.Вставить("ШаблонныеДанныеСуществуют_в_Регистре",Ложь);
		Возврат Истина;
	КонецЕсли;
		
	ПараметрыИзменения = новый Структура(ФиксированныеПараметры);
	ПараметрыИзменения.Вставить("ХранилищеШаблонныхДанных",ХранилищеШаблонныхДанных);
	ПараметрыИзменения = новый ФиксированнаяСтруктура(ПараметрыИзменения);
	ПараметрыИзменения_мод = новый Структура();
	
	Если НЕ ЗаписатьШаблонныеДанныеОбъекта(ПараметрыИзменения, ПараметрыИзменения_мод) Тогда
		СообщениеОшибки = ?(ПараметрыИзменения_мод.Свойство("СообщениеОшибки",СообщениеОшибки),СообщениеОшибки,"");
		МодифицируемыеПараметры.Вставить("СообщениеОшибки",СообщениеОшибки);
		Возврат Ложь;
	КонецЕсли;
		
	МодифицируемыеПараметры.Вставить("ШаблонныеДанныеСуществуют_в_Регистре",Истина);
	Возврат Истина;	
КонецФункции		

Функция УдалитьШаблонныеДанные(ФиксированныеПараметры, МодифицируемыеПараметры = Неопределено ) ЭКСПОРТ
	Если ТипЗнч(МодифицируемыеПараметры) <> Тип("Структура") Тогда
		МодифицируемыеПараметры = новый Структура;
	КонецЕсли;
	
	UIDОбъекта = ФиксированныеПараметры.UIDОбъекта;
	
	НаборЗаписей = СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.UIDОбъекта.Установить(UIDОбъекта);
			
	Попытка
		НаборЗаписей.Записать(Истина);	
		Возврат Истина;
	Исключение
		СообщениеОшибки = ОписаниеОшибки();		
		МодифицируемыеПараметры.Вставить("СообщениеОшибки",СообщениеОшибки);
		Возврат Ложь;		
	КонецПопытки;	
	
КонецФункции

Функция ЭтоХранилищеШаблонныхДанных(ХранилищеДаных) ЭКСПОРТ
	СодержимоеХранилища = КиурСрв.ВернутьЗначениеХранилища(ХранилищеДаных);
	Если ТипЗнч(СодержимоеХранилища) <> Тип("ФиксированнаяСтруктура") Тогда
		Возврат Ложь;
	КонецЕсли;
		
	Возврат СодержимоеХранилища.Свойство("СхемаВыгрузкиШаблонныхДанных") И СодержимоеХранилища.Свойство("ШаблонныеДанные");	
КонецФункции		























